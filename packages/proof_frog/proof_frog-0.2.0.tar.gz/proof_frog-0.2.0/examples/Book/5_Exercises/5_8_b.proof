import 'examples/Primitives/PRG.primitive';
import 'examples/Games/Misc/BitStringSampling.game';
import 'examples/Book/5_Exercises/5_8_b.scheme';
import 'examples/Book/5_Exercises/5_8_ThreePartBitStringSampling.game';
import 'examples/Games/PRG/Security.game';

Reduction R1(PRG G, PRG_5_8_b H) compose Security(G) against Security(H).Adversary {
    BitString<H.lambda + H.stretch> Query() {
        BitString<3 * H.lambda> result = challenger.Query();
        
        BitString<H.lambda> x = result[0 : H.lambda];
        BitString<H.lambda> y = result[H.lambda : 2*H.lambda];

        return x || y;
    }
}

Reduction R2(PRG G, PRG_5_8_b H, Int lambda) compose ThreePartBitStringSampling(lambda) against Security(H).Adversary {
    BitString<H.lambda + H.stretch> Query() {
        BitString<3 * H.lambda> result = challenger.Query();

        BitString<H.lambda> x = result[0 : H.lambda];
        BitString<H.lambda> y = result[H.lambda : 2*H.lambda];

        return x || y;
    }
}

Reduction R3(PRG_5_8_b H, Int lambda) compose BitStringSampling(lambda, lambda) against Security(H).Adversary {
    BitString<H.lambda + H.stretch> Query() {
        return challenger.Query();
    }
}

proof:

let:
    Int lambda;
    PRG G = PRG(lambda, 2 * lambda);
    PRG_5_8_b H = PRG_5_8_b(G);

assume:
    Security(G);
    ThreePartBitStringSampling(lambda);
    BitStringSampling(lambda, lambda);

theorem:
    Security(H);

games:
    Security(H).Real against Security(H).Adversary;

    Security(G).Real compose R1(G, H) against Security(H).Adversary;

    Security(G).Random compose R1(G, H) against Security(H).Adversary;

    ThreePartBitStringSampling(lambda).SampleDirectly compose R2(G, H, lambda) against Security(H).Adversary;

    ThreePartBitStringSampling(lambda).Concatenate compose R2(G, H, lambda) against Security(H).Adversary;

    BitStringSampling(lambda, lambda).Concatenate compose R3(H, lambda) against Security(H).Adversary;

    BitStringSampling(lambda, lambda).SampleDirectly compose R3(H, lambda) against Security(H).Adversary;

    Security(H).Random against Security(H).Adversary;
